CMAKE_MINIMUM_REQUIRED(VERSION 2.6)

PROJECT("RST")

SET(CMAKE_ALLOW_LOOSE_LOOP_CONSTRUCTS TRUE)

# --- global definitions ---

SET(CPP_NAME "rst")
SET(RST_VERSION_MAJOR "0")
SET(RST_VERSION_MINOR "4")
SET(RST_VERSION_PATCH "0")
SET(RST_VERSION "${RST_VERSION_MAJOR}.${RST_VERSION_MINOR}.${RST_VERSION_PATCH}")

# --- dependency handling ---

FIND_PACKAGE(RSC REQUIRED)
SET(CMAKE_MODULE_PATH ${CMAKE_INSTALL_PREFIX}/share/cmake/Modules ${CMAKE_MODULE_PATH} ${RSC_CMAKE_MODULE_PATH})

FIND_PACKAGE(ProtocolBuffers REQUIRED)
IF(NOT PROTOBUF_LIBRARY)
    MESSAGE(FATAL_ERROR "protobuf library not found")
ENDIF()
IF(NOT PROTOBUF_PROTOC_EXECUTABLE)
    MESSAGE(FATAL_ERROR "protoc executable not found")
ENDIF()

# --- protocol ---

FILE(GLOB_RECURSE PROTO_FILES
     RELATIVE "${CMAKE_CURRENT_SOURCE_DIR}"
     "${CMAKE_CURRENT_SOURCE_DIR}/proto/[^.]*.proto")

# --- make absolute paths from PROTO_FILES ---
FOREACH(FILE ${PROTO_FILES})
    GET_FILENAME_COMPONENT(ABS_FILE ${FILE} ABSOLUTE)
    SET(RSTFILES ${RSTFILES} ${ABS_FILE})
ENDFOREACH()

SET(PROTO_ROOT "${CMAKE_SOURCE_DIR}/proto")

ADD_SUBDIRECTORY(cpp)
ADD_SUBDIRECTORY(java)
ADD_SUBDIRECTORY(python)

# --- install protocol anyways for backwards compatibility ---

MACRO(INSTALL_FILES_WITH_DIRECTORY FILE_LIST)

    FOREACH(HEADER ${${FILE_LIST}})
        STRING(REGEX MATCH "(.*)[/\\]" DIR ${HEADER})
        INSTALL(FILES ${HEADER} DESTINATION share/rst/${DIR})
    ENDFOREACH(HEADER)

ENDMACRO()

INSTALL_FILES_WITH_DIRECTORY(PROTO_FILES)

# --- cmake configuration file ---

# decide on export macros
IF(WIN32)
    SET(RST_CFLAGS "/DRST_EXPORT=__declspec(dllimport)")
ELSE()
    SET(RST_CFLAGS "-DRST_EXPORT=")
ENDIF()

CONFIGURE_FILE(RSTConfig.cmake.in ${CMAKE_CURRENT_BINARY_DIR}/RSTConfig.cmake @ONLY)
INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/RSTConfig.cmake DESTINATION share/rst)
EXPORT(TARGETS ${RSC_NAME} FILE "${CMAKE_BINARY_DIR}/RSTDepends.cmake")
INSTALL(EXPORT RSTDepends
        DESTINATION "share/rst")

# --- package ---

SET(CPACK_PACKAGE_VENDOR "CoR-Lab Bielefeld University")
SET(CPACK_PACKAGE_VERSION_MAJOR ${RST_VERSION_MAJOR})
SET(CPACK_PACKAGE_VERSION_MINOR ${RST_VERSION_MINOR})
SET(CPACK_PACKAGE_VERSION_PATCH ${RST_VERSION_PATCH})
SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/COPYING")

SET (CPACK_DEBIAN_PACKAGE_VERSION ${RST_VERSION_MAJOR}.${RST_VERSION_MINOR})
SET (CPACK_DEBIAN_PACKAGE_MAINTAINER "Sebastian Wrede (swrede@techfak.uni-bielefeld.de)")
SET (CPACK_DEBIAN_PACKAGE_DESCRIPTION "Type specifications for Robotics and Cognitive Systems specified in Google Protocol Buffer's IDL format.")
SET (CPACK_DEBIAN_PACKAGE_PRIORITY "optional")
SET (CPACK_DEBIAN_PACKAGE_SECTION "devel")
SET (CPACK_DEBIAN_ARCHITECTURE ${CMAKE_SYSTEM_PROCESSOR})
SET (CPACK_DEBIAN_PACKAGE_DEPENDS "libprotoc-dev, protobuf-compiler, cmake-data, cmake")

INCLUDE(CPack)
